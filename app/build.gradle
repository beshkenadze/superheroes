apply plugin: 'com.android.application'

def versionMajor = 0
def versionMinor = 1
def versionPatch = 0
def versionBuild = 0

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        applicationId "net.beshkenadze.mysuperheroes"
        minSdkVersion 18
        targetSdkVersion 22

        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}" + (versionPatch == 0 ? "" : ".${versionPatch}")

        Properties marvelApi = new Properties()
        marvelApi.load(new File(project.getRootDir(), 'marvel.api').newDataInputStream())

        def publicKey = marvelApi.getProperty('marvel.api_public_key')
        def privateKey = marvelApi.getProperty('marvel.api_private_key')

        //api
        buildConfigField "String", "ROOT_URL", "\"https://gateway.marvel.com/\""
        buildConfigField "String", "API_URL", "\"http://gateway.marvel.com/v1/public/\""

        buildConfigField "String", "API_PUBLIC_KEY", "\"" + publicKey + "\""
        buildConfigField "String", "API_PRIVATE_KEY", "\"" + privateKey + "\""
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    // Support
    compile 'com.android.support:appcompat-v7:22.1.0'
    compile 'com.android.support:support-v13:22.1.0'
    compile 'com.android.support:recyclerview-v7:22.1.0'
    compile 'com.android.support:cardview-v7:22.1.0'

    // Network
    compile 'com.squareup.retrofit:retrofit:1.9.0'
    compile 'com.squareup.okhttp:okhttp:2.3.0'
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.3.0'

    // Other
    compile 'com.jakewharton.timber:timber:3.0.2'
    compile 'joda-time:joda-time:2.7'
    compile 'com.jakewharton:butterknife:6.1.0'
    compile 'io.reactivex:rxandroid:0.24.0'
    compile 'com.github.bumptech.glide:glide:3.6.0'

    // Database
    compile 'io.realm:realm-android:0.80.2'
}
